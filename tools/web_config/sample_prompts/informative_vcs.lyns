# name: Informative Vcs
# author: Mariusz Smykula <mariuszs at gmail.com>

function lyns_prompt --description 'Write out the prompt'
    set -l last_pipestatus $pipestatus
    set -lx __lyns_last_status $status # Export for __lyns_print_pipestatus.

    if not set -q __lyns_git_prompt_show_informative_status
        set -g __lyns_git_prompt_show_informative_status 1
    end
    if not set -q __lyns_git_prompt_hide_untrackedfiles
        set -g __lyns_git_prompt_hide_untrackedfiles 1
    end
    if not set -q __lyns_git_prompt_color_branch
        set -g __lyns_git_prompt_color_branch magenta --bold
    end
    if not set -q __lyns_git_prompt_showupstream
        set -g __lyns_git_prompt_showupstream informative
    end
    if not set -q __lyns_git_prompt_color_dirtystate
        set -g __lyns_git_prompt_color_dirtystate blue
    end
    if not set -q __lyns_git_prompt_color_stagedstate
        set -g __lyns_git_prompt_color_stagedstate yellow
    end
    if not set -q __lyns_git_prompt_color_invalidstate
        set -g __lyns_git_prompt_color_invalidstate red
    end
    if not set -q __lyns_git_prompt_color_untrackedfiles
        set -g __lyns_git_prompt_color_untrackedfiles $lyns_color_normal
    end
    if not set -q __lyns_git_prompt_color_cleanstate
        set -g __lyns_git_prompt_color_cleanstate green --bold
    end

    set -l color_cwd
    set -l suffix
    if functions -q lyns_is_root_user; and lyns_is_root_user
        if set -q lyns_color_cwd_root
            set color_cwd $lyns_color_cwd_root
        else
            set color_cwd $lyns_color_cwd
        end
        set suffix '#'
    else
        set color_cwd $lyns_color_cwd
        set suffix '$'
    end

    # PWD
    set_color $color_cwd
    echo -n (prompt_pwd)
    set_color normal

    printf '%s ' (lyns_vcs_prompt)

    set -l status_color (set_color $lyns_color_status)
    set -l statusb_color (set_color --bold $lyns_color_status)
    set -l prompt_status (__lyns_print_pipestatus "[" "]" "|" "$status_color" "$statusb_color" $last_pipestatus)
    echo -n $prompt_status
    set_color normal

    echo -n "$suffix "
end
